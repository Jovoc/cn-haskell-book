# 函数语法

## 模式匹配

这个章节将带大家了解`Haskell`中一些很棒的语法结构，我们先从模式匹配开始讲起。模式匹配包括
指定数据应该遵循的模式和解析数据是否符合给定模式。

当定义函数时，你可以定义不同的函数体，且为之定义不同的模式。这确实十分有趣，简单，而且可
读性强。你可以使用模式匹配任何数据类型，如数值、字符、列表、元组等。现在举一些简单的例子，
检查下所给出的数值是否是`7`。

```haskell
lucky :: (Integral a) => a -> String  
lucky 7 = "LUCKY NUMBER SEVEN!"  
lucky x = "Sorry, you're out of luck, pal!"
```

我们调用`lucky`时，模式会自上而下检查是否匹配某个模式。匹配的模式中的函数体将会被执行。只
有`7`才能通过上述中的第一个模式。如果匹配不上，将会去尝试匹配第二个模式，`x`可以匹配任何类型
的值。使用`if`语句同样可以实现上述功能。如果希望函数分别输出1到5不同的值，但对于其它值，输出
`"Not between 1 and 5"`。没有模式匹配，我们就不得不使用一个较为复杂的`if-else`的表达语句。
但如果有模式匹配：

```haskell
sayMe :: (Integral a) => a -> String  
sayMe 1 = "One!"  
sayMe 2 = "Two!"  
sayMe 3 = "Three!"  
sayMe 4 = "Four!"  
sayMe 5 = "Five!"  
sayMe x = "Not between 1 and 5"  
```

如果把最后一个模式移到最顶上的位置，作为第一条模式，任何值都会被匹配，那就总是输出
`"Not between 1 and 5"`，从而也就没有机会再去匹配其它的模式。

还记得我们之前实现的`factorial`函数吗？其返回值`n`由`product [1..n]`返回，我们也可以使
用数学公式，以递归的方式实现它。我们先定义`0`的`factorial`数为`1`，然后说明任何一个正整数
的`factorial`数是这个整数与之前的一个`factorial`数的乘积。现在看如何将它翻译成`Haskell`
的术语。

```haskell
factorial :: (Integral a) => a -> a  
factorial 0 = 1  
factorial n = n * factorial (n - 1)  
```

这是我们第一次使用递归的方式定义函数。递归在`Haskell`中十分有用，在后续的章节中，我们会
更深入地了解它。简单地说，当我们要得到`3`的`factorial`数时，它会尝试去计算`3 * factorial 2`。
`2`的`factorial`数又是`2 * factorial 1`。所以我们得到了`3 * (2 * factorial 1)`，`factorial 1`
又是`1 * factorial 0`，然后是`3 * (2 * (1 * factorial 0))`。这里有个小技巧，我们定义`0`的
`factorial`数为`1`，当匹配到`0`时，返回`1`。最终得到的结果是`3 * (2 * (1 * 1))`。如果
我们把第二个模式移到第一个模式的上面，它将匹配所有的数值，包括`0`，那它将永远计算下去。这
也是之所以模式的顺序十分重要，通常都是这将一些特殊的模式放在第一个，更普通的放在后面。

模式匹配也可能会失败。比如我们定义下面的函数：

```haskell
charName :: Char -> String  
charName 'a' = "Albert"  
charName 'b' = "Broseph"  
charName 'c' = "Cecil"  
```

如果我们尝试使用该模式中没有定义的输入，如下图:

```haskell
ghci> charName 'a'  
"Albert"  
ghci> charName 'b'  
"Broseph"  
ghci> charName 'h'  
"*** Exception: tut.hs:(53,0)-(55,21): Non-exhaustive patterns in function charName  
```

它说我们没有包含`h`的模式，就直接抛出了异常。当我们在编写需要匹配的模式时，尽可能地去匹配
所有的模式，这样程序就不会因为无效的输入而报错。

在元组上使用模式匹配也同样适用。如果我们想要编写一个函数，这个函数的作用在于将二维空间中
的向量相加操作。进行向量操作时，需要各自将`x`轴的值相加，将`y`轴的值相加。在不知道模式
匹配的情况下，我们可能会使用下面的表示：

```haskell
addVectors :: (Num a) => (a, a) -> (a, a) -> (a, a)  
addVectors a b = (fst a + fst b, snd a + snd b)  
```

确实会正常工作，但我们有更好的办法去实现它。让我们修改这个函数，并用模式匹配去实现它。

```haskell
addVectors :: (Num a) => (a, a) -> (a, a) -> (a, a)  
addVectors (x1, y1) (x2, y2) = (x1 + x2, y1 + y2)  
```

正如所想的一样，确实更好。注意，这里的模式可以匹配所有的模式。函数`addVectors`的类型为
`addVectors :: (Num a) => (a, a) -> (a, a) - > (a, a)`，所以能确保得到两对值作为参数。

`fst`和`snd`用于提取对值中的元素。那三元组又如何。虽然没有现成的函数，但我们可以自己定义

```haskell
first :: (a, b, c) -> a  
first (x, _, _) = x  

second :: (a, b, c) -> b  
second (_, y, _) = y  

third :: (a, b, c) -> c  
third (_, _, z) = z  
```

`_`用于表示列表中的各种表示，不需要关心它的具体信息，所以我们只写了一个`_`。

还可以在列表推导式中使用模式匹配，如下：

```haskell
ghci> let xs = [(1,3), (4,3), (2,4), (5,3), (5,6), (3,1)]  
ghci> [a+b | (a,b) <- xs]  
[4,7,6,8,11,4]   
```

如果其中一个模式匹配失败了，它将进行直接进行下一个模式匹配。

列表本身也可以用于模式匹配中。你可以使用`[]`去匹配空的列表或者任何包含`:`及空列表去匹配
一个空的列表，而`[1,2,3]`其实就是`1:2:3:[]`的语法糖。当然你们也可以使用前面的去匹配。
`x:xs`匹配一个列表，列表的表头为`x`，表尾为`xs`，如果列表中只有一个元素，那么`xs`则去匹配
了一个空的列表。

> 注意：`x:xs`我们已经使用了很多次，特别是在递归函数中，但其中包含`:`的模式要求匹配的列表
的长度至少为1。

如果你想将前三个元素绑定到三个变量，将其剩余部分绑定到另一个变量。你只要使用`x:y:z:zs`，
它将匹配的列表的长度至少为3。

现在我们知道如何将模式匹配运用在列表上，现在我们来实现`head`函数

```haskell
head' :: [a] -> a  
head' [] = error "Can't call head on an empty list, dummy!"  
head' (x:_) = x  
```

试下它是否正常执行：

```haskell
ghci> head' [4,5,6]  
4  
ghci> head' "Hello"  
'H'  
```

非常好。请注意，如果你想将元素绑定到多个变量(就算其中一个是`_`也不会生效)，就必须在括号内
加入其它变量。同样也要注意`error`函数，使用一字符串作为参数，并生成一个运行时的错误，其字符串
参数信息将作为错误的提示信息。它会引起程序中止，过多地使用这个函数不太友好，但我们知道`head`
函数作用在空的列表中并不起作用，那就可以用一下下。

下面定义一个比较杂的函数，用于以英语书面的形式表达列表中的元素：

```haskell
tell :: (Show a) => [a] -> String  
tell [] = "The list is empty"  
tell (x:[]) = "The list has one element: " ++ show x  
tell (x:y:[]) = "The list has two elements: " ++ show x ++ " and " ++ show y  
tell (x:y:_) = "This list is long. The first two elements are: " ++ show x ++ " and " ++ show y  
```

因为它考虑到了空的列表，单个值的列表，两个值的列表或者更多值的列表，所以这个函数是安全可
靠的。注意，`(x:[])`和`(x:y:[])`可以使用`[x]`和`[x,y]`(使用它的语言糖表示，就不需要使
用到括号)，但我们不能使用方括号重写`(x:y:_)`，因为它匹配长度等于2或大于2的列表。

我们已经使用列表推导式，实现了自己的`length`方法。现在我们使用模式匹配和一点递归技巧来实
现它。

```haskell
length' :: (Num b) => [a] -> b  
length' [] = 0  
length' (_:xs) = 1 + length' xs
```

这和我们之前写的`factorial`函数相似。首先我们定义对于已知参数的返回值是什么。这也被叫为
`边界条件`。然后第二个模式将列表拆分成一个表头和一个表尾。一个列表的长度为`1`加上其表尾的
长度。我们使用`_`去匹配表头，因为我们不需要去关心它是什么。留意函数是否匹配所的有列表，可以
这么去理解，第一个模式匹配到空的列表，第二个模式匹配到除空列表的所有列表。

让我们来看看如果调用`length "ham"`又会怎样。首先，会检查是否是空的列表，如果不是空列表，
将去尝试匹配第二个模式。第二个匹配成功后，返回是`1 + length' "am"`，原因在于我们将列表
分拆成一个表头和表尾。那么`length "am"`又如果，一样的，返回`1 + length' "m"`。马上我们得出
结论，返回了`1 + (1 + length' "m")`，其中`length' "m"`又是`1 + length' ""`(也可以写成
`1 + length' []`)，又因为我们定义了`length' []`为`0`，那最后的计算过程得到是`1 + (1 + (1 + 0))`。

下面我们来实现`sum`函数。我们知道空的列表的和值是为`0`，根据这，就可以写下一个模式。大家都
应该知道，一个列表的和是其表头元素的值加上其表尾的和。如下面代码所示：

```haskell
sum' :: (Num a) => [a] -> a  
sum' [] = 0  
sum' (x:xs) = x + sum' xs
```

模式匹配还有一种表达方式。这是一种比较便利的方式，在匹配模式绑定变量的同时，还能保存对带个未
拆分成的值的引用。使用一个变量名，后面跟上`@`，再接着要匹配的模式。比如`xs@(x:y:ys)`，这个模式
的效果和`x:y:ys`相同，但我们可以通过`xs`变量引用当前列表，而不需要要函数体里使用`x:y:ys`进行
列表的还原。简单看下面的例子：

```haskell
capital :: String -> String  
capital "" = "Empty string, whoops!"  
capital all@(x:xs) = "The first letter of " ++ all ++ " is " ++ [x]  
```

```haskell
ghci> capital "Dracula"  
"The first letter of Dracula is D"  
```

正常使用下，当在函数体里又要重新用到整个列表时，使用这种模式匹配可避免在程序体中使用多个
绑定变量来表示整个列表的情形。

值得注意的一件事。我们不能在模式匹配中使用`++`。如果你在模式匹配时，使用了`(xs ++ ys)`，
那么第一个列表和第二个列表中的值又都是什么。可见，不能被成推测。可以使用`(xs ++ [x,y,z])`，
也就是`(xs ++ [x])`，会进行成功匹配。但基于列表的性质，我们不会这样去写。

## 有一只拦路虎

除了保证数值是否符合某种形式的模式匹配，还有一种用于测试数据属性是否为真或假的方法，那就
是`guards`。听起来好像就是`if`语句。但最重要的一点在于，在使用多个条件时，使用`guards`
可读性更强，而且和模式匹配配合起来也更搭。

通过使用`guards`一个函数的例子来说明它的语法。我们将创建一个函数，根据你的BMI值，返回给你
一些提示信息。BMI值等于你的体重除以你身高的平方。如果你的BMI值低于18.5，它提示你太轻了。
如果你的BMI值在18.5到25，那你就是正常的。25到30之间，表示你太重了，如果超过30的话，你就是
一个庞然大物了。下面是这个函数(其中不涉及到具体值的计算，只是返回一些信息)：

```haskell
bmiTell :: (RealFloat a) => a -> String  
bmiTell bmi  
    | bmi <= 18.5 = "You're underweight, you emo, you!"  
    | bmi <= 25.0 = "You're supposedly normal. Pffft, I bet you're ugly!"  
    | bmi <= 30.0 = "You're fat! Lose some weight, fatty!"  
    | otherwise   = "You're a whale, congratulations!"  
```

在函数名和参数名后使用管道符`|`可以表示`guards`。通常我们会新起一行，并向右缩进。一个`guard`
是一个布尔类型的表达式。如果计算为`True`，那相关的程序将会执行。如果计算为`False`，会跳过
进入下一下个`guard`中进行布尔值测试。如果你的BMI值为`24.3`，第一次会检查是不是小于或等于
`18.5`，显然不是，那它进入那一个`guard`，对该`guard`中的测试语句进行测试，显然`24.3 < 25.0`，
所以第二个字符串被返回。

这就好像是在过程式语言中的一个大大的`if`语句，不同点在于使用`|`可读性强点。过于复杂的`if else`
语句一直被诟病。但有时问题在于，你可能没有想到所有的可能性结果，那`guards`就是一个不错的选择。

通常最后一个`guard`是`otherwise`。`otherwise`其实可以理解为`otherwise = True`，它可以捕获
所有。这个和模式很相似。模式匹配判断输入是否符合，而`guard`用于判断一些布尔值的条件。如果所有
`guard`都匹配失败了，而且我们没有提供一个`otherwise`来捕获所有，那和模式匹配一样，如果没有匹配
到的模式，那么就反产生错误。

当然你可以在`guard`中使用更多的参数。使用一个有计算功能的BMI函数。修改上述函数，以重量和高度为
参数

```haskell
bmiTell :: (RealFloat a) => a -> a -> String  
bmiTell weight height  
    | weight / height ^ 2 <= 18.5 = "You're underweight, you emo, you!"  
    | weight / height ^ 2 <= 25.0 = "You're supposedly normal. Pffft, I bet you're ugly!"  
    | weight / height ^ 2 <= 30.0 = "You're fat! Lose some weight, fatty!"  
    | otherwise                 = "You're a whale, congratulations!"  
```

看下我是否胖了点...

```haskell
ghci> bmiTell 85 1.90  
"You're supposedly normal. Pffft, I bet you're ugly!"
```

嘻嘻，看样子我好像不胖。

注意，在函数名和参数名后面、第1个`guard`之前，我们没有使`=`符号。一些新手遇到一些语法问题，
就是因为情不自禁地加上了`=`。

另一个十分简单的例子。让我们来实现我们自己的`max`函数，它的作为是比较两个输入，输出较大者。

```haskell
max' :: (Ord a) => a -> a -> a  
max' a b   
    | a > b     = a  
    | otherwise = b  
```

也可以在一行上将所有的`guard`写完，我建议还是不要那么做，因为它的可读性太差了。就算是给非常
短的函数也不要这样写。但对于演示，我们可以以下面的形式写这`max`函数

```haskell
max' :: (Ord a) => a -> a -> a  
max' a b | a > b = a | otherwise = b  
```

没有一点的可读性！来，让我们使用`guard`来实现自己的`compare`函数吧。

```haskell
myCompare :: (Ord a) => a -> a -> Ordering  
a `myCompare` b  
    | a > b     = GT  
    | a == b    = EQ  
    | otherwise = LT
```

```haskell
ghci> 3 `myCompare` 2  
GT  
```

> 注意，不仅我们可以以中缀表达式的形式调用函数，还可以使用倒引号来标识函数。有时候，这样
更易于阅读。

## Where!?


在前面的例子中，我们定义了一个计算BMI值，且返回信息的函数，如下：

```haskell
bmiTell :: (RealFloat a) => a -> a -> String  
bmiTell weight height  
    | weight / height ^ 2 <= 18.5 = "You're underweight, you emo, you!"  
    | weight / height ^ 2 <= 25.0 = "You're supposedly normal. Pffft, I bet you're ugly!"  
    | weight / height ^ 2 <= 30.0 = "You're fat! Lose some weight, fatty!"  
    | otherwise                   = "You're a whale, congratulations!"  
```

请仔细看，这里我们将BMI值的计算公式重复了三次，也就是说，我们将相同的表达式计算了三次，那
就肯定产生了一个想法，如何让表达式只计算一次。只要将表达式绑定在一个变量，再使用变量代替
表达式就可以了。我们可以做出如下修改：

```haskell
bmiTell :: (RealFloat a) => a -> a -> String  
bmiTell weight height  
    | bmi <= 18.5 = "You're underweight, you emo, you!"  
    | bmi <= 25.0 = "You're supposedly normal. Pffft, I bet you're ugly!"  
    | bmi <= 30.0 = "You're fat! Lose some weight, fatty!"  
    | otherwise   = "You're a whale, congratulations!"  
    where bmi = weight / height ^ 2  
```

我们在`guard`后面使用了关键字`where`(比较好的做法是将`where`缩进至与`|`符号同列)，然后
定义了几个一个变量`bmi`。这个变量在进行判断时是有效的，而且我们不需要重复书写相同的表达式。
如果我们要修改我们的计算公式，也只需要修改一个地方就可以。使用这些统一的变量名也提高了程序
的可读性，也提高了程序的速度，原因在于`bmi`的计算只进行了一次。我们再尝试做一些修改，正
如下面的函数所示：

```haskell
bmiTell :: (RealFloat a) => a -> a -> String  
bmiTell weight height  
    | bmi <= skinny = "You're underweight, you emo, you!"  
    | bmi <= normal = "You're supposedly normal. Pffft, I bet you're ugly!"  
    | bmi <= fat    = "You're fat! Lose some weight, fatty!"  
    | otherwise     = "You're a whale, congratulations!"  
    where bmi = weight / height ^ 2  
          skinny = 18.5  
          normal = 25.0  
          fat = 30.0  
```

在`where`条件后定义的变量，在进行判断时始终有效，且只有该函数内有效，所以不会污染在这个命名
空间下的其它函数。注意，所有的变量都处于同一列，各占一行。如果我们没有按照这种写法，`Haskell`
会不知道这些变量到底属于哪里。

`where`定义的变量在不同的模式匹配之间是不能共享的。如果你希望所有的模式中都使用到这个变量，
请在全局域中定义。

当然，你也可以有`where`中使用模式匹配，下面是重写后的代码：

```haskell
...  
where bmi = weight / height ^ 2  
      (skinny, normal, fat) = (18.5, 25.0, 30.0)
```

再来看下面这个函数，得到姓和名的第一个单词(这里是外国人的姓和名哈)

```haskell
initials :: String -> String -> String  
initials firstname lastname = [f] ++ ". " ++ [l] ++ "."  
    where (f:_) = firstname  
          (l:_) = lastname    
```

在这里，我们直接使用模式匹配去匹配函数参数(当然我们可以写的更简短些)，但这里只是为了说明
有`where`后确实可以这么做。

就好像我们在`where`块中定义了一个常量，你也可以定义函数。现在让我们编写一个函数，该函数
使用一个重量和高度的对值，返回一个BMI值的列表。

```haskell
calcBmis :: (RealFloat a) => [(a, a)] -> [a]  
calcBmis xs = [bmi w h | (w, h) <- xs]  
    where bmi weight height = weight / height ^ 2  
```

这什么我们要引入`bmi`这个函数？因为我们并不仅只是用来处理由函数参数值传进来的数据。我们需
要在列表推导式中，对列表中所有的对值进行处理。

`where`也可以用于嵌套结构中。一个常用的套路是先定义一个函数，然后在`where`从句后定义一些
帮助函数。每一个帮助函数都有一个与之对应的`where`从句。

## 总要做点什么吧

和`where`绑定相似的是`let`绑定。`where`绑定是语法上将数据绑定在变量上，包括`guard`中，
函数内所有皆可见。`let`绑定是可以在任何地方被访问，并进行赋值，但如果是本地变量，则不能
延伸到`guard`中访问。和`Haskell`中的其它结构一样，`let`绑定是用于将值绑定到变量，还可
以用于模式匹配。我们来看看。这个函数用于计算圆柱体的表面积：

```haskell
cylinder :: (RealFloat a) => a -> a -> a  
cylinder r h =
    let sideArea = 2 * pi * r * h  
        topArea = pi * r ^2  
    in  sideArea + 2 * topArea  
```

使用的语法格式为`let <bindings> in <expression>`。在`let`从句中定义的变量，可以在`in`
从句中被访问。正如你所想，我们也可以在`where`从句后定义。注意，变量的定义还是需要缩进至
同一列。那与`where`中使用变量定义有什么不同吗？从目前来看，`let`是将变量定义在前面，在后面
再使用这些变量，`where`从句则与之相反。

真正不同之处在于，`let`是表达式，而`where`只是一种语法结构。记住在`Haskell`中`if`和`if else`
都是表达式。

```haskell
ghci> [if 5 > 3 then "Woo" else "Boo", if 'a' > 'b' then "Foo" else "Bar"]  
["Woo", "Bar"]  
ghci> 4 * (if 10 > 5 then 10 else 0) + 2  
42  
```

我们也可以使用`let`绑定

```haskell
ghci> 4 * (let a = 9 in a + 1) + 2  
42  
```

在本地作用域中，也可以使用函数进行计算。

```haskell
ghci> [let square x = x * x in (square 5, square 3, square 2)]  
[(25,9,4)]  
```

如果我们想要在一行书写多个变量的绑定，显然不能把它们对齐成一列，可以使用`;`进行分隔。

```haskell
ghci> (let a = 100; b = 200; c = 300 in a*b*c, let foo="Hey "; bar = "there!" in foo ++ bar)  
(6000000,"Hey there!")  
```

在最后一个绑定后面，如果你愿意，也可以加上`;`，也可以不加。如我们之前所说，你可以在`let`
绑定时进行模式匹配。对于将元组分解成多个元素，并将它们绑定到变量，确实十分有用。

```haskell
ghci> (let (a,b,c) = (1,2,3) in a+b+c) * 100  
600  
```

同样，你也可以在列表推导式中的使用`let`绑定。现在重写之前通过对值计算BMI值的方法

```haskell
calcBmis :: (RealFloat a) => [(a, a)] -> [a]  
calcBmis xs = [bmi | (w, h) <- xs, let bmi = w / h ^ 2]
```

通过将`let`绑定应用于列表推导式中，我们可以确定，它不用于过滤列表，只能用于绑定变量。
在列表推导式中定义的变量，可以在`|`之前的作用域中访问到。在变量绑定后面可以进行一些过滤
操作，所以我们可以过滤出胖的人BMI值

```haskell
calcBmis :: (RealFloat a) => [(a, a)] -> [a]  
calcBmis xs = [bmi | (w, h) <- xs, let bmi = w / h ^ 2, bmi >= 25.0]
```

我们不能在`(w, h) <- xs`使用`bmi`变量，因为它的定义在`let`绑定前。(但可以在`|`中使用)

`in`从句可以被省略，你可以直接将函数和常量定义在`GHCI`中，如果你这么做，那这些变量在整个
当前会话始终有效。

```haskell
ghci> let zoot x y z = x * y + z  
ghci> zoot 3 9 2  
29  
ghci> let boot x y z = x * y + z in boot 3 4 2  
14  
ghci> boot  
<interactive>:1:0: Not in scope: `boot'  
```

既然`let`绑定那么棒，为什么不全部代替使用`where`绑定。这是因为`let`绑定是表达式，只有在
它们本地的作用域中有效，而不能在`guard`中使用。所以有些人选择使用`where`绑定，这样虽然变量
是在后面定义的，但前面也可以使用到，这样的话，函数体更靠近于它的函数名称和类型定义，使整个
程序更易读。

## `case`表达式

许多命令式的语言(C,C++,Java等)都有`case`的语法结构，就算你没有实际地用过这些语言，你大概
也知道有`case`语法。`case`语句是为特定的数值的变量执行特定的代码块，可以有时
也需要为一些没有特定情况指定要执行的代码块。

`Haskell`中的`case`也使用这些语言中对`case`的概念。正如它名字所暗示的，`case`从句也是一
个表达式，和`if else`表达式及`let`绑定一样。我们不仅可以基于一些特定值情况计算表达式，也
可以进行模式匹配。没错，可以使用一个变量值，然后进行模式匹配。你之前有听说过，还可以基于一
个值执行特定的代码块吗？还有，参数的模式匹配在函数定义中的使用，这是`Haskell`给我们的`case`
语法糖啊。下面那个代码表示的功能是一样的：

```haskell
head' :: [a] -> a  
head' [] = error "No head for empty lists!"  
head' (x:_) = x  
```

```haskell
head' :: [a] -> a  
head' xs = case xs of [] -> error "No head for empty lists!"  
                      (x:_) -> x  
```

正如你所见，`case`从句的结构十分简单：

```haskell
case expression of pattern -> result  
                   pattern -> result  
                   pattern -> result  
                   ...  
```

`expression`部分可以用于模式匹配，进行模式匹配的逻辑也与我们期望的一样，先进行第一个模式
匹配，如果失败了则顺到下一个模式匹配中，如果没有匹配到模式，则报运行时错误。

鉴于对函数参数的模式只能在函数定义中使用，而`case`表达式可以在任何地方使用。比如：

```haskell
describeList :: [a] -> String  
describeList xs = "The list is " ++ case xs of [] -> "empty."  
                                               [x] -> "a singleton list."   
                                               xs -> "a longer list."
```

这对于一些中间变量的匹配十分有用。又因为在函数中进行模式匹配是`case`表达式的语法糖，所以
也可以像下面这么定义函数：

```haskell
describeList :: [a] -> String  
describeList xs = "The list is " ++ what xs  
    where what [] = "empty."  
          what [x] = "a singleton list."  
          what xs = "a longer list."  
```
